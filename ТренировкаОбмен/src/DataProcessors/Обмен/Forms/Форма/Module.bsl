
&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МойПлан.Ссылка КАК Ссылка,
		|	МойПлан.Код КАК Код
		|ИЗ
		|	ПланОбмена.МойПлан КАК МойПлан
		|ГДЕ
		|	НЕ МойПлан.ЭтотУзел";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ЭтотУзел = ПланыОбмена.МойПлан.ЭтотУзел();
	//Каталог = "E:\1с";
	Каталог = Объект.КаталогОбмена;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекУзел = ВыборкаДетальныеЗаписи.Ссылка;
		
		ЗаписьXML = Новый ЗаписьXML;
		ИмяФайла = СтрШаблон("%1/%2_%3.xml", Каталог, ЭтотУзел.Код, ТекУзел.Код);
		ЗаписьXML.ОткрытьФайл(ИмяФайла);
		
		ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
		ЗаписьСообщения.НачатьЗапись(ЗаписьXML, ТекУзел);
		
		Выборка = ПланыОбмена.ВыбратьИзменения(ТекУзел, ЗаписьСообщения.НомерСообщения);
		
		Пока Выборка.Следующий() Цикл
			Данные = Выборка.Получить();
			ЗаписатьXML(ЗаписьXML, Данные);
			
		КонецЦикла;
		ЗаписьСообщения.ЗакончитьЗапись();
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Прочитать(Команда)
	ПрочитатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МойПлан.Ссылка КАК Ссылка,
		|	МойПлан.Код КАК Код
		|ИЗ
		|	ПланОбмена.МойПлан КАК МойПлан
		|ГДЕ
		|	НЕ МойПлан.ЭтотУзел";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ЭтотУзел = ПланыОбмена.МойПлан.ЭтотУзел();
	//Каталог = "E:\1с";
	Каталог = Объект.КаталогОбмена;

	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекУзел = ВыборкаДетальныеЗаписи.Ссылка;
		
		ИмяФайла = СтрШаблон("%1/%2_%3.xml", Каталог, ТекУзел.Код, ЭтотУзел.Код);
		
		Файл = Новый Файл(ИмяФайла);
		
		Если НЕ Файл.Существует() Тогда
			Продолжить;
		КонецЕсли;
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ИмяФайла);
		ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
		ЧтениеСообщения.НачатьЧтение(ЧтениеXML);
		ПланыОбмена.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);
		
		Пока ВозможностьЧтенияXML(ЧтениеXML) Цикл 
			Данные = ПрочитатьXML(ЧтениеXML);
			Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;
			Данные.ОбменДанными.Загрузка = Истина;
			Данные.Записать();
			
		КонецЦикла;
				
		ЧтениеСообщения.ЗакончитьЧтение();
	КонецЦикла;
КонецПроцедуры       

&НаКлиенте
Процедура ФонЗадание(Команда)
	ФонЗаданиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ФонЗаданиеНаСервере()
	
	ФоновоеСообщение = ФоновыеЗадания.Выполнить("ТестФонЗаданий.СообщенияПользователюВФоне",, "СообщенияФон", "Тест сообщений в фоне");
	
	УИДФЗ = ФоновоеСообщение.УникальныйИдентификатор;
	
	Пока Истина Цикл
		ФоновоеСообщение = ФоновоеСообщение.ОжидатьЗавершенияВыполнения(10);
		Если ФоновоеСообщение.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			// продолжаем ждать завершения
			Продолжить;
		Иначе
			// фоновое задание завершено
			Сообщения = ФоновоеСообщение.ПолучитьСообщенияПользователю();
			
			Для каждого Сообщение из Сообщения Цикл
				Сообщение.Сообщить();
			КонецЦикла;
			Прервать; 
		КонецЕсли;
		
	КонецЦикла;
	
	Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(УИДФЗ);
	Сообщение = Новый СообщениеПользователю;
	
	Сообщение.Текст = СтрШаблон("Фоновое задание: %1", Задание.Наименование); 
	Сообщение.Сообщить();
КонецПроцедуры

&НаКлиенте
Процедура РегЗадание(Команда)
	РегЗаданиеНаСервере();
КонецПроцедуры

&НаСервере
Процедура РегЗаданиеНаСервере()
	
	Расписание = Новый РасписаниеРегламентногоЗадания();
	Расписание.ПериодПовтораВТечениеДня = 100;
	Расписание.ПериодПовтораДней = 1;
	
	Задание = РегламентныеЗадания.СоздатьРегламентноеЗадание("РегЗадание");
	Задание.Расписание = Расписание;
	Задание.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСериализатор(Команда)
	ЗаписатьСериализаторНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСериализаторНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МойПлан.Ссылка КАК Ссылка,
		|	МойПлан.Код КАК Код
		|ИЗ
		|	ПланОбмена.МойПлан КАК МойПлан
		|ГДЕ
		|	НЕ МойПлан.ЭтотУзел";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ЭтотУзел = ПланыОбмена.МойПлан.ЭтотУзел();
	
	Каталог = Объект.КаталогОбмена;

	СтруктураНоменклатурыПолучателя = Новый Структура("Артикул, Наименование, УИД, Тип");
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекУзел = ВыборкаДетальныеЗаписи.Ссылка;
		
		ЗаписьXML = Новый ЗаписьXML;
		ИмяФайла = СтрШаблон("%1/%2_%3(Сериализатор).xml", Каталог, ЭтотУзел.Код, ТекУзел.Код);
		ЗаписьXML.ОткрытьФайл(ИмяФайла);
		
		ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
		ЗаписьСообщения.НачатьЗапись(ЗаписьXML, ТекУзел);
		
		Выборка = ПланыОбмена.ВыбратьИзменения(ТекУзел, ЗаписьСообщения.НомерСообщения);
		
		Пока Выборка.Следующий() Цикл
			Данные = Выборка.Получить();
			Если ТипЗнч(Данные) = Тип("СправочникОбъект.Номенклатура") Тогда
				ЗаполнитьЗначенияСвойств(СтруктураНоменклатурыПолучателя, Данные);
				СтруктураНоменклатурыПолучателя.Артикул = Данные.Артикул2;
				СтруктураНоменклатурыПолучателя.Тип = "Справочник.Номенклатура";
				СтруктураНоменклатурыПолучателя.УИД = Строка(Данные.Ссылка.УникальныйИдентификатор());
				СериализаторXDTO.ЗаписатьXML(ЗаписьXML, СтруктураНоменклатурыПолучателя);
			Иначе
				СериализаторXDTO.ЗаписатьXML(ЗаписьXML, Данные);
				
			КонецЕсли;
			
		КонецЦикла;
		ЗаписьСообщения.ЗакончитьЗапись();
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьСериализатор(Команда)
	ПрочитатьСериализаторНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСериализаторНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МойПлан.Ссылка КАК Ссылка,
		|	МойПлан.Код КАК Код
		|ИЗ
		|	ПланОбмена.МойПлан КАК МойПлан
		|ГДЕ
		|	НЕ МойПлан.ЭтотУзел";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ЭтотУзел = ПланыОбмена.МойПлан.ЭтотУзел();
	Каталог = Объект.КаталогОбмена;

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекУзел = ВыборкаДетальныеЗаписи.Ссылка;
		
		ИмяФайла = СтрШаблон("%1/%2_%3(Сериализатор).xml", Каталог, ТекУзел.Код, ЭтотУзел.Код);
		
		Файл = Новый Файл(ИмяФайла);
		
		Если НЕ Файл.Существует() Тогда
			Продолжить;
		КонецЕсли;
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ИмяФайла);
		ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
		ЧтениеСообщения.НачатьЧтение(ЧтениеXML);
		ПланыОбмена.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);
		
		Пока СериализаторXDTO.ВозможностьЧтенияXML(ЧтениеXML) Цикл 
			
			Данные = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
			
			Если ТипЗнч(Данные) = Тип("Структура") И Данные.Тип = "Справочник.Номенклатура" Тогда
				Ссылка = Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор(Данные.УИД));
				ОбъектНоменклатура = Ссылка.ПолучитьОбъект();
				
				Если ОбъектНоменклатура = Неопределено Тогда 
					ОбъектНоменклатура = Справочники.Номенклатура.СоздатьЭлемент();
					ОбъектНоменклатура.УстановитьСсылкуНового(Ссылка);
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ОбъектНоменклатура, Данные);
				ОбъектНоменклатура.ОбменДанными.Загрузка = Истина;
				ОбъектНоменклатура.Записать();
			Иначе
				Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;
				Данные.ОбменДанными.Загрузка = Истина;
				Данные.Записать();
			КонецЕсли;
		КонецЦикла;
				
		ЧтениеСообщения.ЗакончитьЧтение();
	КонецЦикла;
	
КонецПроцедуры



















